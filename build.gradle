plugins {
  id 'application'
  id 'checkstyle'
  id 'pmd'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.0'
  id 'org.flywaydb.flyway' version '9.16.1'
  id 'org.springframework.boot' version '2.7.10'
  id 'org.owasp.dependencycheck' version '8.1.2'
  id 'com.github.ben-manes.versions' version '0.44.0'
  id 'org.sonarqube' version '4.0.0.2929'
}

group = 'uk.gov.hmcts.reform'
version = '0.0.1'

pmd {
  toolVersion = "6.51.0"
  reportsDir = file("$project.buildDir/reports/pmd")
  ruleSetFiles = files("config/pmd/ruleset.xml")
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

checkstyle {
  toolVersion = '9.2.1'
  configFile = new File(rootDir, "checkstyle.xml")
  maxWarnings = 0
}

ext {
  lombokVersion = '1.18.26'
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  // Specifies if the build should be failed if a CVSS score above a specified level is identified.
  // range of 0-10 fails the build, anything greater and it doesn't fail the build
  failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
  suppressionFile = 'dependency-check-suppressions.xml'
  analyzers {
    // Disable scanning of .NET related binaries
    assemblyEnabled = false
  }
}

repositories {
  mavenLocal()
  mavenCentral()
  maven { url 'https://jitpack.io' }
}

dependencyManagement {
    dependencies {

        //CVE-2022-25857, CVE-2022-38749, CVE-2022-38750, CVE-2022-38751, CVE-2022-38752, CVE-2022-41854
        dependencySet(group: 'org.yaml', version: '1.33') {
            entry 'snakeyaml'
        }
    }
}

jacocoTestReport {
  executionData(test)
  reports {
    xml.required = true
    csv.required = false
    xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
  }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport


sonarqube {
  properties {
    property "sonar.projectName", "sds-toffee-recipes-service"
    property "sonar.projectKey", "sds-toffee-recipes-service"
    property "sonar.dependencyCheck.reportPath", "${project.buildDir}/reports/dependency-check-report.xml"
    property "sonar.coverage.exclusions", "**/config/**, **Exception**"
  }
}

dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

ext["rest-assured.version"] = '4.5.0'

dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'
  implementation group: 'org.postgresql', name: 'postgresql', version: '42.5.4'
  implementation group: 'org.flywaydb', name: 'flyway-core', version: '9.14.1'

  implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '6.0.1'
  implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: '1.6.15'

  compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
  annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', {
    exclude group: 'junit', module: 'junit'
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
  testImplementation group: 'io.rest-assured', name: 'rest-assured'
  testImplementation group: 'com.typesafe', name: 'config', version: '1.4.2'
  testImplementation "org.testcontainers:junit-jupiter:1.17.6"
  testImplementation "org.testcontainers:postgresql:1.17.6"
}

mainClassName = 'uk.gov.hmcts.reform.rhubarb.recipes.Application'

bootJar {
  archiveFileName = 'sds-toffee-recipes-service.jar'

  manifest {
    attributes('Implementation-Version': project.version.toString())
  }
}

sourceSets {
  functional {
    java {
      compileClasspath += main.output
      runtimeClasspath += main.output
      srcDir file('src/functional/java')
    }
    resources.srcDir file('src/functional/resources')
  }
}

configurations {
  functionalImplementation.extendsFrom testImplementation
  functionalRuntimeOnly.extendsFrom testRuntimeOnly
}

task highLevelDataSetup {
  println "highLevelDataSetup task has been called"
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functional.output.classesDirs
  classpath = sourceSets.functional.runtimeClasspath

  useJUnitPlatform {
    includeTags "SmokeTest"
  }
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
  testClassesDirs = sourceSets.functional.output.classesDirs
  classpath = sourceSets.functional.runtimeClasspath

  useJUnitPlatform()
}

test {
  useJUnitPlatform()
}

wrapper {
  distributionType = Wrapper.DistributionType.ALL
}
